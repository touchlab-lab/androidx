import androidx.build.AndroidXComposePlugin
import androidx.build.LibraryGroups
import androidx.build.Publish
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinNativeCompile

plugins {
    id("AndroidXPlugin")
    id("AndroidXComposePlugin")
    id("kotlin-multiplatform")
    id "org.jetbrains.kotlin.native.cocoapods"
}

buildDir = new File(projectDir, ".build")
version = "1.0"

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)
dependencies {
    kotlinPlugin(project(":compose:compiler:compiler"))
    kotlinNativeCompilerPluginClasspath(project(":compose:compiler:compiler-hosted"))
}

repositories {
    mavenLocal()
}

kotlin {
    def onPhone = System.getenv("SDK_NAME")?.startsWith("iphoneos")
//    if (onPhone) {
//        iosArm64("ios")
//    } else {
//        iosX64("ios")
//    }
    ios()
    jvm()

    cocoapods {
        noPodspec()

        summary = "Example of Kotlin Gradle Plugin Playground"
        homepage = "https://github.com/touchlab/CompilerPluginPlayground"
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation(project(":compose:native"))

                implementation(project(":compose:ui:ui"))
                implementation(project(":compose:ui:ui-graphics"))
                implementation(project(":compose:foundation:foundation-layout"))
                implementation(project(":compose:runtime:runtime"))
            }
        }
        nativeMain {
            dependsOn(commonMain)
            dependencies {
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core") {
                    version { strictly("1.5.0-native-mt") }
                }
            }
        }
        iosMain {
            dependsOn(nativeMain)
            dependencies {

            }
        }
        jvmMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.skikoCurrentOs)
                implementation(project(":compose:desktop:desktop"))
            }
        }
    }
}

task runDemo(type: JavaExec) {
    dependsOn(":compose:desktop:desktop:jar")
    main = "AppKt"
    systemProperty("skiko.fps.enabled", "true")
    def compilation = kotlin.jvm().compilations["main"]
    classpath = compilation.output.allOutputs + compilation.runtimeDependencyFiles
}

tasks.withType(KotlinCompile) {
    kotlinOptions {
    }
}

tasks.withType(KotlinNativeCompile) {
    kotlinOptions {
        freeCompilerArgs += [
            "-P", "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true",
            "-Xdisable-phases=VerifyBitcode",
            // TODO: need the proper plugin flag for the binary for the final link.
//            "-Xplugin=" + project.rootProject.ext["outDir"] + "/androidx/compose/compiler/compiler-hosted/build//libs/compiler-hosted-1.1.0-alpha01.jar"
        ]
    }
}
